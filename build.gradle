plugins {
    id 'net.researchgate.release' version '2.8.0'
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

ext {
    grpcVersion = '1.26.0'
    gitUrl = "https://github.com/dialogs/java-bot-sdk"
    issueTrackerUrl = "https://github.com/dialogs/java-bot-sdk/issues"
    libraryDescription = "Bot SDK for java developers"
    bintrayUser = System.env.bintrayUser != null ? System.env.bintrayUser : ""
    bintrayPassword = System.env.bintrayPassword != null ? System.env.bintrayPassword : ""
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "http://dialog.bintray.com/maven" }
}

afterReleaseBuild.dependsOn bintrayUpload

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

release {
    preCommitText = System.env.COMMIT_PREFIX != null ? System.env.COMMIT_PREFIX : ""
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId group
            artifactId artifactId
            version version
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayPassword
    override = false
    configurations = ['archives']

    pkg {
        userOrg = 'dialog'
        repo = 'maven'
        name = artifactId
        desc = libraryDescription
        websiteUrl = 'https://dlg.im'
        vcsUrl = gitUrl
        licenses = ["MIT"]
        issueTrackerUrl = issueTrackerUrl
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                if (hasProperty('bintrayGpgPassword')) {
                    bintray.pkg.version.gpg.passphrase = bintrayGpgPassword
                }
            }
        }
    }
}

dependencies {
    compile group: 'im.dlg', name: 'code', version: 'v1.10.1', { exclude group: 'io.grpc' }
    compile group: 'com.typesafe', name: 'config', version: '1.3.2'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.9.1'
    compile group: 'io.grpc', name: 'grpc-all', version: grpcVersion
    compile group: 'io.grpc', name: 'grpc-context', version: grpcVersion
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    compile group: 'io.grpc', name: 'protoc-gen-grpc-java', version: grpcVersion, ext: 'pom'
    compile 'org.apache.httpcomponents:httpclient:4.5.6'
    compile 'org.asynchttpclient:async-http-client:2.6.0'
    compile group: 'org.javatuples', name: 'javatuples', version: '1.2'
    compile 'io.netty:netty-tcnative-boringssl-static:2.0.17.Final'
    compile group: 'org.bouncycastle', name: 'bcprov-jdk16', version: '1.45'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.27'
    compile group: 'net.javacrumbs.future-converter', name: 'future-converter-java8-guava', version: '1.2.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
